// This file is compiled inside Function{Plot rate}

// Input: inAddr, points, ratingStr

if or (!var inAddr > 0, !var inId > 0) {
  chat "&cYou must be inside an occupied plot."
  sound "Anvil Land" 0.3 1.0 invokers_location
  exit
}

if (var homeAddr == var plotAddr -1) {
  chat "&cYou cannot rate your own plot."
  sound "Pig Idle" 1.0 1.0 invokers_location
  exit
}

var plotAddr = var inAddr
var cd = 0
function "Plot download"
function "Plot postprocess"

// If download is on CD
if (var cd == 0) {
  chat "&6&lTRY AGAIN &eYou already downloaded a plot in the last 4 ticks."
  sound "Anvil Land" 0.3 1.0 invokers_location
  exit
}

if (var plotId == var id) {
  chat "&cYou cannot rate your own plot."
  sound "Pig Idle" 1.0 1.0 invokers_location
  exit
}

// Check if player has already rated this plot
var rated = 1
var plotAddr = var inAddr
function "Plot rate read"

// If the player has already rated this plot
if (var rated == 1) {
  chat "&cYou already rated this plot."
  sound "Anvil Land" 0.3 1.0 invokers_location
  exit
}

// Send message to ChatSocket
// Event input: reqId, reqAddr
globalvar reqId = var id
globalvar reqAddr = var plotAddr
var cd = 0
function "Send plot rate" true

// If function is on CD
if (var cd == 0) {
  chat "&6&lTRY AGAIN &eSomeone else used this function within the last 4 ticks."
  sound "Anvil Land" 0.3 1.0 invokers_location
  exit
}

// Input: plotAddr, rated (bool)
var rated = 1
function "Plot rate write"

chat "&aYou rated &oPlot &b&o#%var.player/plotAddr%&a. &fRating: &c%var.player/points% &7(%var.player/ratingStr%&7)"
sound "Level Up" 0.7 1.0 invokers_location